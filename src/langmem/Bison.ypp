%error-verbose
%locations

%parse-param{mem::ast::node::File* ast}
%parse-param{mem::st::SymbolTable* symbols}
%parse-param{mem::log::Logger* logger}
%parse-param{mem::fs::File* file}

%code requires
{

#define PASS (0);
#include <stdio.h>
#include <string.h>
#ifndef INCLUDED_BISON_HPP
#define INCLUDED_BISON_HPP
#include "langmem/Bison.hpp"
#endif
#include "langmem/Tokenizer.hpp"
#include "mem/ast/node/ArrayType.hpp"
#include "mem/ast/node/BinaryOp.hpp"
#include "mem/ast/node/Block.hpp"
#include "mem/ast/node/BracketOp.hpp"
#include "mem/ast/node/Call.hpp"
#include "mem/ast/node/CastOp.hpp"
#include "mem/ast/node/Class.hpp"
#include "mem/ast/node/Decorator.hpp"
#include "mem/ast/node/Dot.hpp"
#include "mem/ast/node/EnumType.hpp"
#include "mem/ast/node/Field.hpp"
#include "mem/ast/node/File.hpp"
#include "mem/ast/node/FinalId.hpp"
#include "mem/ast/node/For.hpp"
#include "mem/ast/node/Func.hpp"
#include "mem/ast/node/If.hpp"
#include "mem/ast/node/New.hpp"
#include "mem/ast/node/Number.hpp"
#include "mem/ast/node/PointerType.hpp"
#include "mem/ast/node/Return.hpp"
#include "mem/ast/node/Root.hpp"
#include "mem/ast/node/String.hpp"
#include "mem/ast/node/Text.hpp"
#include "mem/ast/node/Tuple.hpp"
#include "mem/ast/node/TupleType.hpp"
#include "mem/ast/node/UnaryOp.hpp"
#include "mem/ast/node/Use.hpp"
#include "mem/ast/node/VarAssign.hpp"
#include "mem/ast/node/VarDecl.hpp"
#include "mem/ast/node/While.hpp"
#include "mem/fs/File.hpp"
#include "mem/fs/FileManager.hpp"
#include "mem/fs/position/Range.hpp"
#include "mem/log/Logger.hpp"
#include "mem/st/SymbolTable.hpp"
#include "mem/st/util.hpp"
#include "mem/number.hpp"

using namespace mem;


#define YYLTYPE fs::position::Range
#define YYLTYPE_IS_DECLARED 1

#define YYLLOC_DEFAULT(Current, Rhs, N) \
   do \
   { \
      if (YYID (N)) \
      { \
         (Current).setLineStart(YYRHSLOC (Rhs, 1).LineStart()); \
         (Current).setLineEnd(YYRHSLOC (Rhs, 1).LineEnd()); \
         (Current).setColumnStart(YYRHSLOC (Rhs, 1).ColumnStart()); \
         (Current).setColumnEnd(YYRHSLOC (Rhs, N).ColumnEnd()); \
         (Current).sFile(YYRHSLOC(Rhs, 1)._file); \
      } \
      else \
      { \
         (Current).setLineStart(YYRHSLOC (Rhs, 0).LineStart()); \
         (Current).setLineEnd(YYRHSLOC (Rhs, 0).LineEnd()); \
         (Current).setColumnStart(YYRHSLOC (Rhs, 0).ColumnEnd()); \
         (Current).setColumnEnd(YYRHSLOC (Rhs, 0).ColumnEnd()); \
         (Current).sFile(NULL); \
      } \
   } \
   while (YYID (0))

extern langmem::Tokenizer gTOKENIZER;
}

%code {
   langmem::Tokenizer gTOKENIZER;

   int yylex()
   {
      langmem::Token t = gTOKENIZER.getNextToken();

      switch (t.Kind())
      {
         case T_ID:
         case T_OP:
         case T_STRING:
         case T_LITERAL_NUMBER:
            std::string* str = new std::string();
            str->assign(t.Value());
            yylval.text = str;
            break;
      }
      yylloc = t.Location();
      return t.Kind();
   }

   void yyerror(ast::node::Node* ast,
      mem::st::SymbolTable* symbols,
      ::mem::log::Logger* logger, // Type ambiguity for gcc-c++ 4.2.1 on FreeBSD
      fs::File* file,
      const char *s)
   {
      fs::position::Position* pos = new fs::position::Position();
      pos->sFile(file);

      log::Message* msg = new log::Error();
      msg->setId("syntax-error");
      msg->setPrimaryText("Syntax error");
      msg->setSecondaryText(s);
      msg->setPosition(pos);
      logger->log(msg);
   }
}

%union
{
   std::string* text;
   mem::ast::node::Node* node;
   mem::ast::node::Text* text_node;
   mem::ast::node::Id* id_node;
   mem::ast::node::BinaryOp* binary_op_node;
   mem::ast::node::Block* block_node;
   mem::ast::node::Decorator* decorator_node;
   mem::ast::node::FinalId* final_id_node;
   mem::ast::node::Func* func_node;
   mem::ast::node::NodeList* node_list;
   mem::ast::node::Type* type_node;
   mem::ast::node::VarDecl* var_decl_node;
   mem::ast::node::While* while_node;
}


%token T_AMPERSAND
%token T_AROBASE
%token T_AND
%token T_BANG
%token T_BANG_EQ
%token T_CLASS
%token T_CLOSE_BRACE
%token T_COLON
%token T_COMMA
%token T_CP
%token T_DEDENT
%token T_DOT
%token T_DOTDOTDOT
%token T_ELSE
%token T_ENUM
%token T_EQ
%token T_FOR
%token T_HASH
%token T_IF
%token <text> T_ID
%token T_INDENT
%token T_INTERFACE
%token T_LBRACKET
%token T_LEFT_BINARY_OP
%token T_LITERAL_FLOAT
%token T_LITERAL_HEX
%token <text> T_LITERAL_INT
%token <text> T_LITERAL_NUMBER
%token T_MINUS_EQ
%token T_NEW
%token T_NEWLINE
%token T_OP
%token T_OPEN_BRACE
%token T_OR
%token T_PASS
%token T_OBJECT
%token T_QUOTE
%token T_PLUS_EQ
%token T_RBRACKET
%token T_RARR
%token T_RETURN
%token T_SEMICOLON
%token T_SINGLE_LINE_COMMENT
%token T_SLASH_EQ
%token T_SPACE
%token T_STAR
%token T_STAR_EQ
%token <text> T_STRING
%token T_USE
%token T_VAR
%token T_WHILE
%token T_WHITESPACE

// Lower precedence first
%left <node> T_AND T_OR
%left <node> T_EQ_EQ T_GT T_GT_EQ T_LT T_RARR T_LT_EQ T_BANQ_EQ
%left <node> T_MINUS T_PLUS
%left <node> T_STAR T_DIV
%left <node> T_LT_LT T_GT_GT T_BIT_OR T_BIT_AND T_XOR
%left <node> T_MUL_MUL T_MODULO
%left <node> T_MINUS_MINUS T_PLUS_PLUS
%left <node> T_OP T_CP T_LBRACKET T_RBRACKET
%left <node> T_DOT T_AROBASE

%type <var_decl_node>  argument
%type <node_list>      ArgumentList
%type <node_list>      ArgumentList_
%type <binary_op_node> binary_expr
%type <binary_op_node> binary_operator
%type <binary_op_node> bool_operator
%type <node>           ClassParent_
%type <binary_op_node> comp_operator
%type <binary_op_node> math_operator
%type <block_node>     block
%type <node>           call
%type <node>           class_declaration
%type <node_list>      ClassMembers_
%type <node>           compound_statement
%type <node>           dot_expr
%type <node>           enum_declaration
%type <node_list>      enum_fields
%type <node>           expr
%type <node_list>      expr_list
%type <node>           for_statement
%type <node>           for_initialization_statement
%type <node>           for_iteration_statement
%type <func_node>      FunctionDeclaration
%type <id_node>        id
%type <node>           if
%type <node>           literal
%type <node_list>      members_declarations
%type <node>           member_declaration
%type <block_node>     newline_or_block
%type <node>           return_statement
%type <node>           ReturnType_
%type <final_id_node>  simple_id
%type <node>           simple_statement
%type <node>           statement
%type <block_node>     statements
%type <node>           tuple_type
%type <id_node>      type_list
%type <text_node>      use
%type <text_node>      use_file
%type <node>           variable_assignment
%type <node>           variable_declaration
%type <while_node>     while
%type <node>           qualified_id

%%

input :
   /* empty */
   | input use
   {
      ast->addChild($2);
   }
   | input class_declaration
   {
      ast->addChild($2);
   }
   | input enum_declaration
   {
      ast->addChild($2);
   }
   | input FunctionDeclaration
   {
      ast->addChild($2);
   }

use:
   T_USE use_file
   {
      $$ = new mem::ast::node::Use();
      $$->setValue($2->Value());
      $$->setPosition(new fs::position::Range(@1, @2));

      delete $2;
   }

use_file :
   T_ID
   {
      $$ = new mem::ast::node::Text();
      $$->setValue($1->c_str());
      delete $1;
   }
   | use_file T_DOT T_ID
   {
      $$->setValue($$->Value() + '.' + $3->c_str());

      delete $3;
   }

eos : T_SEMICOLON


//=============================================================================
// CLASS DECLARATION
//=============================================================================

class_declaration:
   T_CLASS simple_id ClassParent_ T_OPEN_BRACE ClassMembers_ T_CLOSE_BRACE
   {
      mem::ast::node::Class* cls = new mem::ast::node::Class();
      cls->setValue($2->Value());
      cls->setNameNode($2);
      cls->setParentTypeNode($3);
      cls->setMembersNode($5);
      cls->setPosition(new fs::position::Range(@$));

      $$ = cls;
   }

ClassParent_:
   /* empty */
   {
      $$ = NULL;
   }
   | T_LT simple_id
   {
      $$ = $2;
   }

ClassMembers_:
   /* empty */
   {
      $$ = new mem::ast::node::NodeList();
   }
   | members_declarations
   {
      $$ = $1;
   }


//=============================================================================
// ENUM DECLARATION
//=============================================================================

enum_declaration:
   T_ENUM id T_NEWLINE T_INDENT enum_fields
   {
      mem::ast::node::EnumType* e = new mem::ast::node::EnumType();
      e->setNameNode($2);
      e->setValue($2->Value());
      e->setMembersNode($5);

      $$ = e;
   }

enum_fields:
   variable_declaration eos
   {
      $$ = new mem::ast::node::NodeList();
      $$->addChild($1);
   }
   | enum_fields variable_declaration eos
   {
      assert($2 != NULL);
      $$->addChild($2);
   }


//=============================================================================
// FUNCTION DECLARATION
//=============================================================================

FunctionDeclaration:
   // Ex: my_function_name(my_variable :my_type) :my_return_type
   simple_id T_OP ArgumentList_ T_CP ReturnType_ newline_or_block
   {
      $$ = new mem::ast::node::Func();
      $$->setNameNode($1);
      $$->setValue($1->Value());

      // Argument list
      if ($3 != NULL)
      {
         $$->setParamsNode($3);
      }

      // Return type
      if ($5 != NULL)
      {
         $$->setReturnTypeNode(mem::ast::node::cast<mem::ast::node::Id>($5));
      }
      else
      {
         mem::ast::node::FinalId* void_ty = new mem::ast::node::FinalId();
         void_ty->setValue("void");
         $$->setReturnTypeNode(void_ty);
      }

      // Body block
      if ($6 != NULL)
      {
         $$->setBodyNode(mem::ast::node::cast<mem::ast::node::Block>($6));
      }

      $$->setPosition(@$.copy_range());
   }


ArgumentList:
   argument
   {
      $$ = new mem::ast::node::NodeList();
      $$->addChild($1);
      $$->setPosition(@1.copy_range());
   }
   | ArgumentList T_COMMA argument
   {
      $$->addChild($3);
      $$->Position()->append(@3);
   }

ArgumentList_:
   /* empty */
   {
      $$ = NULL;
   }
   | ArgumentList
   {
      $$ = $1;
   }

argument:
   simple_id T_COLON id
   {
      $$ = new mem::ast::node::VarDecl();
      $$->setNameNode($1);
      $$->setTypeNode($3);
      $$->setPosition(new fs::position::Range(@1, @3));
   }

ReturnType_:
   /* EMPTY */
   {
      $$ = NULL;
   }
   | T_RARR id
   {
      $$ = $2;
   }

newline_or_block:
   eos { $$ = NULL; }
   | block

///////////////////////////////////////////////////////////////////////////////
// CONTROL FLOW
///////////////////////////////////////////////////////////////////////////////

//-----------------------------------------------------------------------------
// If
//-----------------------------------------------------------------------------

if:
   // SIMPLE IF
   T_IF expr block
   {
      mem::ast::node::If* n = new mem::ast::node::If();
      n->setConditionNode($2);
      n->setIfBlockNode($3);
      n->setPosition(new fs::position::Range(@1, @2));
      $$ = n;
   }
   // IF / ELSE
   | T_IF expr block T_ELSE block
   {
      mem::ast::node::If* n = new mem::ast::node::If();
      n->setConditionNode($2);
      n->setIfBlockNode($3);
      n->setElseBlockNode($5);
      n->setPosition(new fs::position::Range(@1, @5));
      $$ = n;
   }


//-----------------------------------------------------------------------------
// While
//-----------------------------------------------------------------------------

while:
   T_WHILE expr block
   {
      $$ = new mem::ast::node::While();
      $$->setConditionNode($2);
      $$->setBlockNode($3);
      $$->setPosition(new fs::position::Range(@1, @3));
   }


//-----------------------------------------------------------------------------
// For
//-----------------------------------------------------------------------------

for_initialization_statement:
     variable_declaration
   | variable_assignment
   | expr

for_iteration_statement:
     variable_assignment
   | expr

for_statement:
   T_FOR for_initialization_statement T_SEMICOLON expr T_SEMICOLON for_iteration_statement block
   {
      mem::ast::node::For* n = new mem::ast::node::For();
      n->setInitializationNode($2);
      n->setConditionNode($4);
      n->setIterationNode($6);
      n->setBlockNode(mem::ast::node::cast<mem::ast::node::Block>($7));
      $$ = n;
      $$->setPosition(new fs::position::Range(@1, @7));
   }

block :
   T_OPEN_BRACE statements T_CLOSE_BRACE
   {
      $$ = mem::ast::node::cast<mem::ast::node::Block>($2);
   }

statements :
   /* Empty */
   {
      $$ = new mem::ast::node::Block();
   }
   | statement
   {
      assert ($1 != NULL);
      $$ = new mem::ast::node::Block();
      $$->addChild($1);
   }
   | statements statement
   {
      assert ($$ != NULL);
      $$->addChild($2);
   }

statement:
   expr eos
   {
      $$ = $1;
   }
   | simple_statement eos
   {
      $$ = $1;
   }
   | compound_statement
   {
      $$ = $1;
   }

compound_statement:
     if
   | while
   | for_statement

simple_statement:
     variable_declaration{ $$ = $1;}
   | variable_assignment { $$ = $1;}
   | return_statement { $$ = $1; }

return_statement:
   T_RETURN expr
   {
      mem::ast::node::Return* n = new mem::ast::node::Return();
      n->setValueNode($2);
      $$ = n;
   }


members_declarations :
   member_declaration
   {
      $$ = new mem::ast::node::NodeList();
      $$->addChild($1);
   }
   | members_declarations member_declaration
   {
      assert ($1 != NULL);
      $$->addChild($2);
   }

member_declaration :
   // Ex: my_variable :my_type
   simple_id T_COLON id eos
   {
      mem::ast::node::Field* field = new mem::ast::node::Field();
      field->setPosition(@$.copy_range());
      field->setNameNode($1);
      field->setTypeNode($3);

      $$ = (ast::node::Node*)field;
   }
   | FunctionDeclaration


simple_id :
   T_ID
   {
      $$ = new mem::ast::node::FinalId();
      $$->setPosition(new fs::position::Range(@$));
      $$->setValue($1->c_str());

      delete $1;
   }

id :
   simple_id
   | qualified_id
   {
      $$ = new ast::node::Id();
      $$->setValue($1->Value());
      $$->setPosition(@$.copy_range());
      delete $1;
   }
   // Array type : int*
   | id T_STAR
   {
      mem::ast::node::PointerType* ptr = new mem::ast::node::PointerType();
      ptr->setTypeNode($1);
      $$ = ptr;
      $$->setPosition(@$.copy_range());
   }
   // Unsized array type : [int]
   | T_LBRACKET id T_RBRACKET
   {
      mem::ast::node::ArrayType* arr = new mem::ast::node::ArrayType();
      arr->setTypeNode($2);

      $$ = arr;
      $$->setPosition(@$.copy_range());
   }
   // Sized array type : [int, 5]
   | T_LBRACKET id T_COMMA expr T_RBRACKET
   {
      mem::ast::node::ArrayType* arr = new mem::ast::node::ArrayType();
      arr->setTypeNode($2);
      arr->setLengthNode($4);

      $$ = arr;
      $$->setPosition(@$.copy_range());
   }
   | tuple_type

type_list:
   id
   {
      mem::ast::node::TupleType* t = new mem::ast::node::TupleType();
      t->addChild($1);
      $$ = t;
   }
   | type_list T_COMMA id
   {
      assert ($1 != NULL);
      ((mem::ast::node::TupleType*)$$)->addChild($3);
   }

tuple_type:
   T_OP type_list T_CP
   {
      $$ = $2;
   }

assignment_operator:
   T_EQ | T_PLUS_EQ | T_MINUS_EQ | T_SLASH_EQ | T_STAR_EQ

variable_assignment :
   expr assignment_operator expr
   {
      mem::ast::node::VarAssign* n = new mem::ast::node::VarAssign();
      n->setPosition(@$.copy_range());
      n->setNameNode($1);
      n->setValueNode($3);
      $$ = (ast::node::Node*)n;
   }

variable_declaration :
   // Ex: my_variable :my_type
   T_VAR simple_id T_COLON id
   {
      mem::ast::node::VarDecl* n = new mem::ast::node::VarDecl();
      n->setNameNode($2);
      n->setTypeNode($4);
      n->setPosition(@$.copy_range());
      $$ = (ast::node::Node*)n;
   }
   // Ex: my_variable = my_function()
   | T_VAR simple_id T_EQ expr
   {
      mem::ast::node::Node* ty_place_holder = new mem::ast::node::Node();
      ty_place_holder->setKind(ast::node::MetaKind::PLACE_HOLDER);
      ty_place_holder->setPosition(@4.copy_range());

      mem::ast::node::VarDecl* n = new mem::ast::node::VarDecl();
      n->setNameNode($2);
      n->setTypeNode(ty_place_holder);
      n->setValueNode($4);
      n->setPosition(@$.copy_range());
      $$ = (ast::node::Node*)n;
   }
   // Ex: my_variable :my_type = my_function()
   | T_VAR simple_id T_COLON id T_EQ expr
   {
      mem::ast::node::VarDecl* n = new mem::ast::node::VarDecl();
      n->setNameNode($2);
      n->setTypeNode($4);
      n->setValueNode($6);
      n->setPosition(@$.copy_range());
      $$ = (ast::node::Node*)n;
   }

/* RECURSIVE */
qualified_id:
   simple_id
   {
      mem::ast::node::FinalId* n = new mem::ast::node::FinalId();
      n->setValue($1->Value());
      n->setPosition(@$.copy_range());
      delete $1;
      $$ = ast::node::cast<ast::node::Text>(n);
      assert($$->Position() != NULL);
   }
   | qualified_id T_DOT simple_id
   {
      mem::ast::node::Dot* n = new mem::ast::node::Dot();
      n->setLeftNode($1);
      n->setRightNode($3);
      if (n->Position() != NULL)
      {
         n->Position()->append(@$);
      }
      else
      {
         n->setPosition(@$.copy_range());
      }
      $$ = n;
   }

dot_expr:
   expr T_DOT simple_id
   {
      mem::ast::node::Dot* n = new mem::ast::node::Dot();
      n->setPosition(@$.copy_range());
      n->setLeftNode($1);
      n->setRightNode($3);
      $$ = n;
   }

expr:
   literal
   {
      $$ = $1;
   }
   | dot_expr
   {
      $$ = $1;
   }
   | binary_expr
   {
      $$ = (ast::node::Node*)$1;
   }
   // ex: (1 + 2 + 3)
   | T_OP expr T_CP
   {
      mem::ast::node::NodeList* n = new mem::ast::node::NodeList();
      n->addChild($2);

      $$ = n;
   }
   // ex: (1 + 2, true)
   | T_OP expr_list T_CP
   {
      mem::ast::node::Tuple* n = new mem::ast::node::Tuple();
      n->eat($2);
      $$ = n;

      delete $2;
   }
   | qualified_id
   {
      $$ = ast::node::cast<ast::node::Node>($1);
   }
   | call
   {
      $$ = (ast::node::Node*)$1;
   }
   | T_AMPERSAND expr
   {
      mem::ast::node::UnaryOp* n = new mem::ast::node::UnaryOp();
      n->setKind(ast::node::MetaKind::AMPERSAND);
      n->setValueNode($2);
      $$ = n;
   }
   | T_AROBASE expr
   {
      mem::ast::node::UnaryOp* n = new mem::ast::node::UnaryOp();
      n->setKind(ast::node::MetaKind::DEREF);
      n->setValueNode($2);
      $$ = n;
   }
   // ex: new int
   | T_NEW id
   {
      mem::ast::node::New* n = new mem::ast::node::New();
      n->setTypeNode($2);
      $$ = n;
      $$->setPosition(@$.copy_range());
   }
   // ex: my_var :int
   | expr T_COLON id
   {
      mem::ast::node::CastOp* n = new mem::ast::node::CastOp();
      n->setValueNode($1);
      n->setTypeNode($3);

      $$ = n;
      $$->setPosition(@$.copy_range());
   }
   // ex: my_var[5]
   | expr T_LBRACKET expr T_RBRACKET
   {
      mem::ast::node::BracketOp* n = new mem::ast::node::BracketOp();
      n->setValueNode($1);
      n->setIndexNode($3);
      $$ = n;
   }

bool_operator:
   T_AND
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_AND);
   }
   | T_OR
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_OR);
   }

comp_operator:
   // ex: true == false
   T_EQ_EQ
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_EQ_EQ);
   }
   // ex 1 < 2
   | T_LT
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_LT);
   }
   // ex: 1 <= 2
   | T_LT_EQ
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_LTE);
   }
   // ex: 1 > 2
   | T_GT
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_GT);
   }
   // ex: 1 >= 2
   | T_GT_EQ
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_GTE);
   }
   // ex: 1 != 2
   | T_BANG_EQ
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_NE);
   }

math_operator:
   // ex: 1 + 2
   T_PLUS
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_PLUS);
   }
   | T_MINUS
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_MINUS);
   }
   | T_STAR
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_MUL);
   }
   | T_DIV
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_DIV);
   }
   | T_GT_GT
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_RSHIFT);
   }
   | T_LT_LT
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_LSHIFT);
   }
   | T_BIT_OR
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_BIT_OR);
   }
   | T_BIT_AND
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_BIT_AND);
   }
   | T_XOR
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_XOR);
   }
   | T_MODULO
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_MODULO);
   }

binary_operator:
   bool_operator | comp_operator | math_operator

binary_expr:
   expr binary_operator expr
   {
      mem::ast::node::BinaryOp* n = $2;
      n->setLeftNode($1);
      n->setRightNode($3);
      n->setPosition(@$.copy_range());
      $$ = n;
   }


expr_list :
   expr
   {
      $$ = new mem::ast::node::NodeList();
      $$->addChild($1);
      $$->setPosition(@$.copy_range());

   }
   | expr_list T_COMMA expr
   {
      if ($$ == 0)
      {
         $$ = new mem::ast::node::NodeList();
      }
      $$->addChild($3);
      $$->Position()->append(@3);
   }

call :
   /* ex: my_func() */
   expr T_OP T_CP
   {
      mem::ast::node::Call* n = new mem::ast::node::Call();
      n->setCallerNode($1);

      $$ = n;
      $$->setPosition(new fs::position::Range(@1, @3));
   }
   /* ex: my_func(1, 2, 3) */
   | expr T_OP expr_list T_CP
   {
      mem::ast::node::Call* n = new mem::ast::node::Call();
      n->setCallerNode($1);
      n->setParamsNode($3);

      $$ = n;
      $$->setPosition(new fs::position::Range(@1, @4));
   }

literal :
   T_LITERAL_NUMBER
   {
      int i = 0;
      number::parseIntNumber(*$1, i);
#if 0

      switch (nb_format)
      {
         case 'c':
         {
            mem::st::IntConstant* ic = mem::st::util::getNativeCharConstant(symbols->System(), (char)i);
            ic->setType(symbols->_core_types.CharTy());

            mem::ast::node::Number* c_node = new mem::ast::node::Number();
            c_node->setBoundSymbol(ic);
            c_node->setExprType(symbols->_core_types.CharTy());
            $$ = c_node;
            break;
         }
         case 's':
         {
            mem::st::IntConstant* ic = mem::st::util::getNativeShortConstant(symbols->System(), (short)i);
            ic->setType(symbols->_core_types.ShortTy());

            mem::ast::node::Number* s_node = new mem::ast::node::Number();
            s_node->setBoundSymbol(ic);
            s_node->setExprType(symbols->_core_types.ShortTy());
            $$ = s_node;
            break;
         }
         case 'i':
         {
#endif
            mem::st::IntConstant* ic = mem::st::util::getNativeShortConstant(symbols->System(), i);
            ic->setType(symbols->_core_types.IntTy());

            mem::ast::node::Number* i_node = new mem::ast::node::Number();
            i_node->setBoundSymbol(ic);
            i_node->setExprType(symbols->_core_types.IntTy());
            $$ = i_node;
#if 0
            break;
         }
      }
#endif

      assert ($$ != NULL);

      if ($$ != NULL)
      {
         $$->setPosition(@1.copy_range());
      }
      else
      {
         assert(false);
         //FIXME Couldnt fit this value into a numeric type, so should fail
      }

      delete $1;
   }
   | T_STRING
   {
      mem::ast::node::String* str = new mem::ast::node::String();
      str->setValue($1->c_str());
      str->setPosition(@1.copy_range());

      $$ = str;

      delete $1;
   }
%%

#include "mem/ast/node/Node.hpp"
