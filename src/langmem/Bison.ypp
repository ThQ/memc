%error-verbose
%locations

%parse-param{mem::ast::node::File* ast}
%parse-param{mem::st::SymbolTable* symbols}
%parse-param{mem::log::Logger* logger}
%parse-param{mem::fs::File* file}

%code requires
{

#define PASS (0);
#include <stdio.h>
#include <string.h>
#ifndef INCLUDED_BISON_HPP
#define INCLUDED_BISON_HPP
#include "langmem/Bison.hpp"
#endif
#include "langmem/Tokenizer.hpp"
#include "mem/ast/node/ArrayType.hpp"
#include "mem/ast/node/BinaryOp.hpp"
#include "mem/ast/node/Block.hpp"
#include "mem/ast/node/BracketOp.hpp"
#include "mem/ast/node/Call.hpp"
#include "mem/ast/node/CastOp.hpp"
#include "mem/ast/node/Class.hpp"
#include "mem/ast/node/Decorator.hpp"
#include "mem/ast/node/Dot.hpp"
#include "mem/ast/node/EnumType.hpp"
#include "mem/ast/node/Field.hpp"
#include "mem/ast/node/File.hpp"
#include "mem/ast/node/FinalId.hpp"
#include "mem/ast/node/For.hpp"
#include "mem/ast/node/Func.hpp"
#include "mem/ast/node/FuncType.hpp"
#include "mem/ast/node/If.hpp"
#include "mem/ast/node/New.hpp"
#include "mem/ast/node/Number.hpp"
#include "mem/ast/node/PointerType.hpp"
#include "mem/ast/node/Return.hpp"
#include "mem/ast/node/Root.hpp"
#include "mem/ast/node/String.hpp"
#include "mem/ast/node/Text.hpp"
#include "mem/ast/node/Tuple.hpp"
#include "mem/ast/node/TupleType.hpp"
#include "mem/ast/node/UnaryOp.hpp"
#include "mem/ast/node/Use.hpp"
#include "mem/ast/node/VarAssign.hpp"
#include "mem/ast/node/VarDecl.hpp"
#include "mem/ast/node/While.hpp"
#include "mem/fs/File.hpp"
#include "mem/fs/FileManager.hpp"
#include "mem/fs/position/Range.hpp"
#include "mem/log/Logger.hpp"
#include "mem/st/SymbolTable.hpp"
#include "mem/st/util.hpp"
#include "mem/number.hpp"

using namespace mem;


#define YYLTYPE fs::position::Range
#define YYLTYPE_IS_DECLARED 1

#define YYLLOC_DEFAULT(Current, Rhs, N) \
   do \
   { \
      if (YYID (N)) \
      { \
         (Current).setLineStart(YYRHSLOC (Rhs, 1).LineStart()); \
         (Current).setLineEnd(YYRHSLOC (Rhs, 1).LineEnd()); \
         (Current).setColumnStart(YYRHSLOC (Rhs, 1).ColumnStart()); \
         (Current).setColumnEnd(YYRHSLOC (Rhs, N).ColumnEnd()); \
         (Current).setFile(YYRHSLOC(Rhs, 1).File()); \
      } \
      else \
      { \
         (Current).setLineStart(YYRHSLOC (Rhs, 0).LineStart()); \
         (Current).setLineEnd(YYRHSLOC (Rhs, 0).LineEnd()); \
         (Current).setColumnStart(YYRHSLOC (Rhs, 0).ColumnEnd()); \
         (Current).setColumnEnd(YYRHSLOC (Rhs, 0).ColumnEnd()); \
         (Current).setFile(NULL); \
      } \
   } \
   while (YYID (0))

extern langmem::Tokenizer gTOKENIZER;
}

%code {
   langmem::Tokenizer gTOKENIZER;

   int yylex()
   {
      langmem::Token t = gTOKENIZER.getNextToken();

      switch (t.Kind())
      {
         case T_ID:
         case T_OP:
         case T_STRING:
         case T_LITERAL_NUMBER:
            std::string* str = new std::string();
            str->assign(t.Value());
            yylval.text = str;
            break;
      }
      yylloc = t.Location();
      return t.Kind();
   }

   void yyerror(ast::node::Node* ast,
      mem::st::SymbolTable* symbols,
      ::mem::log::Logger* logger, // Type ambiguity for gcc-c++ 4.2.1 on FreeBSD
      fs::File* file,
      const char *s)
   {
      fs::position::Position* pos = new fs::position::Position();
      pos->setFile(file);

      log::Message* msg = new log::Error();
      msg->setId("syntax-error");
      msg->setPrimaryText("Syntax error");
      msg->setSecondaryText(s);
      msg->setPosition(pos);
      logger->log(msg);
   }
}

%union
{
   std::string* text;
   mem::ast::node::Node* node;
   mem::ast::node::Text* nodeText;
   mem::ast::node::Id* nodeId;
   mem::ast::node::BinaryOp* nodeBinaryOp;
   mem::ast::node::Block* nodeBlock;
   mem::ast::node::Decorator* nodeDecorator;
   mem::ast::node::FinalId* nodeFinalId;
   mem::ast::node::Func* nodeFunc;
   mem::ast::node::NodeList* nodeList;
   mem::ast::node::Type* nodeType;
   mem::ast::node::VarDecl* nodeVarDecl;
   mem::ast::node::While* nodeWhile;
}


%token T_AMPERSAND
%token T_AROBASE
%token T_AND
%token T_BANG
%token T_BANG_EQ
%token T_CLASS
%token T_CLOSE_BRACE
%token T_COLON
%token T_COMMA
%token T_CP
%token T_DEDENT
%token T_DOT
%token T_DOTDOTDOT
%token T_ELSE
%token T_ENUM
%token T_EQ
%token T_FOR
%token T_HASH
%token T_IF
%token <text> T_ID
%token T_INDENT
%token T_INTERFACE
%token T_LBRACKET
%token T_LEFT_BINARY_OP
%token T_LITERAL_FLOAT
%token T_LITERAL_HEX
%token <text> T_LITERAL_INT
%token <text> T_LITERAL_NUMBER
%token T_MINUS_EQ
%token T_NEW
%token T_NEWLINE
%token T_OP
%token T_OPEN_BRACE
%token T_OR
%token T_PASS
%token T_OBJECT
%token T_QUOTE
%token T_PLUS_EQ
%token T_RBRACKET
%token T_RARR
%token T_RETURN
%token T_SEMICOLON
%token T_SINGLE_LINE_COMMENT
%token T_SLASH_EQ
%token T_SPACE
%token T_STAR
%token T_STAR_EQ
%token <text> T_STRING
%token T_USE
%token T_VAR
%token T_WHILE
%token T_WHITESPACE

// Lower precedence first
%left <node> T_AND T_OR
%left <node> T_EQ_EQ T_GT T_GT_EQ T_LT T_RARR T_LT_EQ T_BANQ_EQ
%left <node> T_MINUS T_PLUS
%left <node> T_STAR T_DIV
%left <node> T_LT_LT T_GT_GT T_BIT_OR T_BIT_AND T_XOR
%left <node> T_MUL_MUL T_MODULO
%left <node> T_MINUS_MINUS T_PLUS_PLUS
%left <node> T_OP T_CP T_LBRACKET T_RBRACKET
%left <node> T_DOT T_AROBASE

%type <nodeVarDecl>  Argument
%type <nodeList>     ArgumentList
%type <nodeList>     ArgumentList_
%type <nodeBinaryOp> BinaryExpr
%type <nodeBinaryOp> BinaryOperator
%type <nodeBinaryOp> BoolOperator
%type <node>         ClassParent_
%type <nodeBinaryOp> CompOperator
%type <nodeBinaryOp> MathOperator
%type <nodeBlock>    Block
%type <node>         Call
%type <node>         Class
%type <nodeList>     ClassMembers_
%type <node>         CompoundStatement
%type <node>         DotExpr
%type <node>         Enum
%type <nodeVarDecl>  EnumField
%type <nodeList>     EnumFields
%type <node>         Expr
%type <nodeList>     ExprList
%type <node>         For
%type <node>         ForInitialization
%type <node>         ForIteration
%type <nodeFunc>     FunctionDeclaration
%type <node>         FunctionType
%type <nodeId>       Id
%type <node>         If
%type <node>         Literal
%type <nodeList>     members_declarations
%type <node>         member_declaration
%type <node>         Return
%type <node>         ReturnType_
%type <nodeList>     SimpleIdList
%type <nodeFinalId>  SimpleId
%type <node>         SimpleStatement
%type <node>         Statement
%type <nodeBlock>    Statements
%type <nodeList>     TemplateArguments_
%type <node>         TupleExpr
%type <node>         TupleType
%type <nodeId>       TypeList
%type <nodeId>       TypeList_
%type <nodeText>     Use
%type <nodeText>     UseFile
%type <node>         VariableAssignment
%type <node>         VariableDeclaration
%type <nodeWhile>    While
%type <node>         QualifiedId

%%

File :
   /* empty */
   | File Use
   {
      ast->addChild($2);
   }
   | File Class
   {
      ast->addChild($2);
   }
   | File Enum
   {
      ast->addChild($2);
   }
   | File FunctionDeclaration
   {
      ast->addChild($2);
   }


//=============================================================================
// CLASS DECLARATION
//=============================================================================

Class:
   T_CLASS SimpleId ClassParent_ T_OPEN_BRACE ClassMembers_ T_CLOSE_BRACE
   {
      mem::ast::node::Class* cls = new mem::ast::node::Class();
      cls->setValue($2->Value());
      cls->setNameNode($2);
      cls->setParentTypeNode($3);
      cls->setMembersNode($5);
      cls->setPosition(new fs::position::Range(@$));

      $$ = cls;
   }

ClassParent_:
   /* empty */
   {
      $$ = NULL;
   }
   | T_LT SimpleId
   {
      $$ = $2;
   }

ClassMembers_:
   /* empty */
   {
      $$ = new mem::ast::node::NodeList();
   }
   | members_declarations
   {
      $$ = $1;
   }


members_declarations :
   member_declaration
   {
      $$ = new mem::ast::node::NodeList();
      $$->addChild($1);
   }
   | members_declarations member_declaration
   {
      assert ($1 != NULL);
      $$->addChild($2);
   }

member_declaration :
   // Ex: my_variable :my_type
   SimpleId T_COLON Id T_SEMICOLON
   {
      mem::ast::node::Field* field = new mem::ast::node::Field();
      field->setPosition(@$.copy_range());
      field->setNameNode($1);
      field->setTypeNode($3);

      $$ = (ast::node::Node*)field;
   }
   | FunctionDeclaration
   {
      $$ = $1;
   }


//=============================================================================
// ENUM DECLARATION
//=============================================================================

Enum:
   T_ENUM SimpleId T_OPEN_BRACE EnumFields T_CLOSE_BRACE
   {
      mem::ast::node::FinalId* enum_ty = new mem::ast::node::FinalId();
      enum_ty->setValue("int");

      mem::ast::node::EnumType* e = new mem::ast::node::EnumType();
      e->setNameNode($2);
      e->setValue($2->Value());
      e->setParentTypeNode(enum_ty);
      e->setMembersNode($4);

      $$ = e;
   }

EnumFields:
   EnumField
   {
      $$ = new mem::ast::node::NodeList();
      $$->addChild($1);
   }
   | EnumFields EnumField
   {
      assert($2 != NULL);
      $$ = $1;
      $$->addChild($2);
   }

EnumField:
   SimpleId T_SEMICOLON
   {
      $$ = new mem::ast::node::VarDecl();
      $$->setNameNode($1);
   }


//=============================================================================
// FUNCTION DECLARATION
//=============================================================================

FunctionDeclaration:
   // Ex: my_function_name(my_variable :my_type) :my_return_type
   SimpleId TemplateArguments_ T_OP ArgumentList_ T_CP ReturnType_ Block
   {
      $$ = new mem::ast::node::Func();
      $$->setNameNode($1);
      $$->setValue($1->Value());

      // Argument list
      if ($4 != NULL)
      {
         $$->setParamsNode($4);
      }

      // Return type
      if ($6 != NULL)
      {
         $$->setReturnTypeNode(mem::ast::node::cast<mem::ast::node::Id>($6));
      }
      else
      {
         mem::ast::node::FinalId* void_ty = new mem::ast::node::FinalId();
         void_ty->setValue("void");
         $$->setReturnTypeNode(void_ty);
      }

      // Body block
      if ($7 != NULL)
      {
         $$->setBodyNode(mem::ast::node::cast<mem::ast::node::Block>($7));
      }

      $$->setPosition(@$.copy_range());
   }


ArgumentList:
   Argument
   {
      $$ = new mem::ast::node::NodeList();
      $$->addChild($1);
      $$->setPosition(@1.copy_range());
   }
   | ArgumentList T_COMMA Argument
   {
      $$->addChild($3);
      $$->Position()->append(@3);
   }

ArgumentList_:
   /* empty */
   {
      $$ = new mem::ast::node::NodeList();
   }
   | ArgumentList
   {
      $$ = $1;
   }

Argument:
   SimpleId T_COLON Id
   {
      $$ = new mem::ast::node::VarDecl();
      $$->setNameNode($1);
      $$->setTypeNode($3);
      $$->setPosition(new fs::position::Range(@1, @3));
   }

ReturnType_:
   /* EMPTY */
   {
      $$ = NULL;
   }
   | T_RARR Id
   {
      $$ = $2;
   }

TemplateArguments_:
   /* empty */
   {
      $$ = NULL;
   }
   | T_LBRACKET SimpleIdList T_RBRACKET
   {
      $$ = new mem::ast::node::NodeList();
   }

SimpleIdList:
   SimpleId
   {
      $$ = new mem::ast::node::NodeList();
      $$->addChild($1);
   }
   | SimpleIdList T_COMMA SimpleId
   {
      $1->addChild($3);
   }


///////////////////////////////////////////////////////////////////////////////
// TYPES
///////////////////////////////////////////////////////////////////////////////

TupleType:
   T_OP TypeList T_CP
   {
      $$ = $2;
   }

FunctionType:
   T_OP TypeList_ T_CP T_RARR Id
   {
      mem::ast::node::FuncType* nodeFunc = new mem::ast::node::FuncType();
      nodeFunc->setParamsNode($2);

      $$ = nodeFunc;
   }


///////////////////////////////////////////////////////////////////////////////
// CONTROL FLOW
///////////////////////////////////////////////////////////////////////////////

//-----------------------------------------------------------------------------
// If
//-----------------------------------------------------------------------------

If:
   // SIMPLE IF
   T_IF Expr Block
   {
      mem::ast::node::If* n = new mem::ast::node::If();
      n->setConditionNode($2);
      n->setIfBlockNode($3);
      n->setPosition(new fs::position::Range(@1, @2));
      $$ = n;
   }
   // IF / ELSE
   | T_IF Expr Block T_ELSE Block
   {
      mem::ast::node::If* n = new mem::ast::node::If();
      n->setConditionNode($2);
      n->setIfBlockNode($3);
      n->setElseBlockNode($5);
      n->setPosition(new fs::position::Range(@1, @5));
      $$ = n;
   }


//-----------------------------------------------------------------------------
// While
//-----------------------------------------------------------------------------

While:
   T_WHILE Expr Block
   {
      $$ = new mem::ast::node::While();
      $$->setConditionNode($2);
      $$->setBlockNode($3);
      $$->setPosition(new fs::position::Range(@1, @3));
   }


//-----------------------------------------------------------------------------
// For
//-----------------------------------------------------------------------------

ForInitialization:
     VariableDeclaration
   | VariableAssignment
   | Expr

ForIteration:
     VariableAssignment
   | Expr

For:
   T_FOR ForInitialization T_SEMICOLON Expr T_SEMICOLON ForIteration Block
   {
      mem::ast::node::For* n = new mem::ast::node::For();
      n->setInitializationNode($2);
      n->setConditionNode($4);
      n->setIterationNode($6);
      n->setBlockNode(mem::ast::node::cast<mem::ast::node::Block>($7));
      $$ = n;
      $$->setPosition(new fs::position::Range(@1, @7));
   }


///////////////////////////////////////////////////////////////////////////////
// STATEMENTS
///////////////////////////////////////////////////////////////////////////////

Statement:
   Expr T_SEMICOLON
   {
      $$ = $1;
   }
   | SimpleStatement T_SEMICOLON
   {
      $$ = $1;
   }
   | CompoundStatement
   {
      $$ = $1;
   }

Statements:
   /* Empty */
   {
      $$ = new mem::ast::node::Block();
   }
   | Statement
   {
      assert ($1 != NULL);
      $$ = new mem::ast::node::Block();
      $$->addChild($1);
   }
   | Statements Statement
   {
      assert ($$ != NULL);
      $$->addChild($2);
   }

CompoundStatement:
     If
   | While
   {
      $$ = $1;
   }
   | For

SimpleStatement:
     VariableDeclaration
   | VariableAssignment
   | Return

Return:
   T_RETURN Expr
   {
      mem::ast::node::Return* n = new mem::ast::node::Return();
      n->setValueNode($2);
      $$ = n;
   }

Use:
   T_USE UseFile
   {
      $$ = new mem::ast::node::Use();
      $$->setValue($2->Value());
      $$->setPosition(new fs::position::Range(@1, @2));

      delete $2;
   }

UseFile:
   T_ID
   {
      $$ = new mem::ast::node::Text();
      $$->setValue($1->c_str());
      delete $1;
   }
   | UseFile T_DOT T_ID
   {
      $$->setValue($$->Value() + '.' + $3->c_str());

      delete $3;
   }

VariableAssignment:
   Expr AssignmentOperator Expr
   {
      mem::ast::node::VarAssign* n = new mem::ast::node::VarAssign();
      n->setPosition(@$.copy_range());
      n->setNameNode($1);
      n->setValueNode($3);
      $$ = (ast::node::Node*)n;
   }

VariableDeclaration:
   // Ex: var my_variable :my_type
   T_VAR SimpleId T_COLON Id
   {
      mem::ast::node::VarDecl* n = new mem::ast::node::VarDecl();
      n->setNameNode($2);
      n->setTypeNode($4);
      n->setPosition(@$.copy_range());
      $$ = (ast::node::Node*)n;
   }
   // Ex: my_variable = my_function()
   | T_VAR SimpleId T_EQ Expr
   {
      mem::ast::node::Node* ty_place_holder = new mem::ast::node::Node();
      ty_place_holder->setKind(ast::node::MetaKind::PLACE_HOLDER);
      ty_place_holder->setPosition(@4.copy_range());

      mem::ast::node::VarDecl* n = new mem::ast::node::VarDecl();
      n->setNameNode($2);
      n->setTypeNode(ty_place_holder);
      n->setValueNode($4);
      n->setPosition(@$.copy_range());
      $$ = (ast::node::Node*)n;
   }
   // Ex: my_variable :my_type = my_function()
   | T_VAR SimpleId T_COLON Id T_EQ Expr
   {
      mem::ast::node::VarDecl* n = new mem::ast::node::VarDecl();
      n->setNameNode($2);
      n->setTypeNode($4);
      n->setValueNode($6);
      n->setPosition(@$.copy_range());
      $$ = (ast::node::Node*)n;
   }


///////////////////////////////////////////////////////////////////////////////
// ????
///////////////////////////////////////////////////////////////////////////////

Block:
   T_OPEN_BRACE Statements T_CLOSE_BRACE
   {
      $$ = mem::ast::node::cast<mem::ast::node::Block>($2);
      $$->setPosition(@$.copy_range());
   }

SimpleId:
   T_ID
   {
      $$ = new mem::ast::node::FinalId();
      $$->setPosition(new fs::position::Range(@$));
      $$->setValue($1->c_str());

      delete $1;
   }

Id:
   SimpleId
   {
      $$ = $1;
   }
   | QualifiedId
   {
      $$ = new ast::node::Id();
      $$->setValue($1->Value());
      $$->setPosition(@$.copy_range());
      delete $1;
   }
   // Array type : int*
   | Id T_STAR
   {
      mem::ast::node::PointerType* ptr = new mem::ast::node::PointerType();
      ptr->setTypeNode($1);
      $$ = ptr;
      $$->setPosition(@$.copy_range());
   }
   // Unsized array type : [int]
   | T_LBRACKET Id T_RBRACKET
   {
      mem::ast::node::ArrayType* arr = new mem::ast::node::ArrayType();
      arr->setTypeNode($2);

      $$ = arr;
      $$->setPosition(@$.copy_range());
   }
   // Sized array type : [int, 5]
   | T_LBRACKET Id T_COMMA Expr T_RBRACKET
   {
      mem::ast::node::ArrayType* arr = new mem::ast::node::ArrayType();
      arr->setTypeNode($2);
      arr->setLengthNode($4);

      $$ = arr;
      $$->setPosition(@$.copy_range());
   }
   | TupleType
   {
      $$ = mem::ast::node::cast<mem::ast::node::Id>($1);
   }
   | FunctionType
   {
      $$ = mem::ast::node::cast<mem::ast::node::Id>($1);
   }

TypeList:
   Id
   {
      mem::ast::node::TupleType* t = new mem::ast::node::TupleType();
      t->addChild($1);
      $$ = t;
   }
   | TypeList T_COMMA Id
   {
      assert ($1 != NULL);
      mem::ast::node::cast<mem::ast::node::TupleType>($$)->addChild($3);
   }

TypeList_:
   /* empty */
   {
      $$ = new mem::ast::node::TupleType();
   }
   | TypeList

AssignmentOperator:
   T_EQ | T_PLUS_EQ | T_MINUS_EQ | T_SLASH_EQ | T_STAR_EQ

/* RECURSIVE */
QualifiedId:
   SimpleId
   {
      mem::ast::node::FinalId* n = new mem::ast::node::FinalId();
      n->setValue($1->Value());
      n->setPosition(@$.copy_range());
      delete $1;
      $$ = ast::node::cast<ast::node::Text>(n);
      assert($$->Position() != NULL);
   }
   | QualifiedId T_DOT SimpleId
   {
      mem::ast::node::Dot* n = new mem::ast::node::Dot();
      n->setLeftNode($1);
      n->setRightNode($3);
      if (n->Position() != NULL)
      {
         n->Position()->append(@$);
      }
      else
      {
         n->setPosition(@$.copy_range());
      }
      $$ = n;
   }

DotExpr:
   Expr T_DOT SimpleId
   {
      mem::ast::node::Dot* n = new mem::ast::node::Dot();
      n->setPosition(@$.copy_range());
      n->setLeftNode($1);
      n->setRightNode($3);
      $$ = n;
   }

Expr:
   Literal
   {
      $$ = $1;
   }
   | DotExpr
   {
      $$ = $1;
   }
   | BinaryExpr
   {
      $$ = (ast::node::Node*)$1;
   }
   // ex: (1 + 2, true)
   | QualifiedId
   {
      $$ = ast::node::cast<ast::node::Node>($1);
   }
   | Call
   {
      $$ = ast::node::cast<ast::node::Node>($1);
   }
   // Reference
   // ex: &myVar
   | T_AMPERSAND Expr
   {
      mem::ast::node::UnaryOp* n = new mem::ast::node::UnaryOp();
      n->setKind(ast::node::MetaKind::AMPERSAND);
      n->setValueNode($2);
      $$ = n;
   }
   // Dereference
   // ex: @myPointerVar
   | T_AROBASE Expr
   {
      mem::ast::node::UnaryOp* n = new mem::ast::node::UnaryOp();
      n->setKind(ast::node::MetaKind::DEREF);
      n->setValueNode($2);
      n->setPosition(@$.copy_range());
      $$ = n;
   }
   // ex: new int
   | T_NEW Id
   {
      mem::ast::node::New* n = new mem::ast::node::New();
      n->setTypeNode($2);
      $$ = n;
      $$->setPosition(@$.copy_range());
   }
   // Cast
   // ex: my_var :int
   | Expr T_COLON Id
   {
      mem::ast::node::CastOp* n = new mem::ast::node::CastOp();
      n->setValueNode($1);
      n->setTypeNode($3);

      $$ = n;
      $$->setPosition(@$.copy_range());
   }
   // ex: my_var[5]
   | Expr T_LBRACKET Expr T_RBRACKET
   {
      mem::ast::node::BracketOp* n = new mem::ast::node::BracketOp();
      n->setValueNode($1);
      n->setIndexNode($3);
      $$ = n;
   }
   | TupleExpr

TupleExpr:
   T_OP ExprList T_CP
   {
      if ($2->ChildCount() == 1)
      {
         $$ = $2->getChild(0);
         $$->setParent(NULL);
         // For some reason, we get a segfault if we delete $2
         //delete $2;
      }
      else
      {
         DEBUG_PRINT("Tuple with several expr\n");
         mem::ast::node::Tuple* nodeTuple = new mem::ast::node::Tuple();
         nodeTuple->eat($2);
         nodeTuple->setPosition(@$.copy_range());
         $$ = nodeTuple;

         // See before
         //delete $2;
      }
   }

BoolOperator:
   T_AND
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_AND);
   }
   | T_OR
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_OR);
   }

CompOperator:
   // ex: true == false
   T_EQ_EQ
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_EQ_EQ);
   }
   // ex 1 < 2
   | T_LT
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_LT);
   }
   // ex: 1 <= 2
   | T_LT_EQ
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_LTE);
   }
   // ex: 1 > 2
   | T_GT
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_GT);
   }
   // ex: 1 >= 2
   | T_GT_EQ
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_GTE);
   }
   // ex: 1 != 2
   | T_BANG_EQ
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_NE);
   }

MathOperator:
   // ex: 1 + 2
   T_PLUS
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_PLUS);
   }
   | T_MINUS
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_MINUS);
   }
   | T_STAR
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_MUL);
   }
   | T_DIV
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_DIV);
   }
   | T_GT_GT
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_RSHIFT);
   }
   | T_LT_LT
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_LSHIFT);
   }
   | T_BIT_OR
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_BIT_OR);
   }
   | T_BIT_AND
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_BIT_AND);
   }
   | T_XOR
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_XOR);
   }
   | T_MODULO
   {
      $$ = new mem::ast::node::BinaryOp();
      $$->setKind(ast::node::MetaKind::OP_MODULO);
   }

BinaryOperator:
   BoolOperator | CompOperator | MathOperator

BinaryExpr:
   Expr BinaryOperator Expr
   {
      mem::ast::node::BinaryOp* n = $2;
      n->setLeftNode($1);
      n->setRightNode($3);
      n->setPosition(@$.copy_range());
      $$ = n;
   }

ExprList:
   Expr
   {
      $$ = new mem::ast::node::NodeList();
      $$->addChild($1);
      $$->setPosition(@$.copy_range());
   }
   | ExprList T_COMMA Expr
   {
      $$->addChild($3);
      $$->Position()->append(@3);
   }

Call:
   /* ex: my_func() */
   Expr T_OP T_CP
   {
      mem::ast::node::Call* n = new mem::ast::node::Call();
      n->setCallerNode($1);
      n->setParamsNode(new mem::ast::node::NodeList());

      $$ = n;
      $$->setPosition(new fs::position::Range(@1, @3));
   }
   /* ex: my_func(1, 2, 3) */
   | Expr T_OP ExprList T_CP
   {
      mem::ast::node::Call* n = new mem::ast::node::Call();
      n->setCallerNode($1);
      n->setParamsNode($3);

      $$ = n;
      $$->setPosition(new fs::position::Range(@1, @4));
   }

Literal:
   T_LITERAL_NUMBER
   {
      int i = 0;
      number::parseIntNumber(*$1, i);
#if 0

      switch (nb_format)
      {
         case 'c':
         {
            mem::st::IntConstant* ic = mem::st::util::getNativeCharConstant(symbols->System(), (char)i);
            ic->setType(symbols->_core_types.CharTy());

            mem::ast::node::Number* c_node = new mem::ast::node::Number();
            c_node->setBoundSymbol(ic);
            c_node->setExprType(symbols->_core_types.CharTy());
            $$ = c_node;
            break;
         }
         case 's':
         {
            mem::st::IntConstant* ic = mem::st::util::getNativeShortConstant(symbols->System(), (short)i);
            ic->setType(symbols->_core_types.ShortTy());

            mem::ast::node::Number* s_node = new mem::ast::node::Number();
            s_node->setBoundSymbol(ic);
            s_node->setExprType(symbols->_core_types.ShortTy());
            $$ = s_node;
            break;
         }
         case 'i':
         {
#endif
            mem::st::IntConstant* ic = mem::st::util::getNativeShortConstant(symbols->System(), i);
            ic->setType(symbols->_core_types.IntTy());

            mem::ast::node::Number* i_node = new mem::ast::node::Number();
            i_node->setBoundSymbol(ic);
            i_node->setExprType(symbols->_core_types.IntTy());
            $$ = i_node;
#if 0
            break;
         }
      }
#endif

      assert ($$ != NULL);

      if ($$ != NULL)
      {
         $$->setPosition(@1.copy_range());
      }
      else
      {
         assert(false);
         //FIXME Couldnt fit this value into a numeric type, so should fail
      }

      delete $1;
   }
   | T_STRING
   {
      mem::ast::node::String* str = new mem::ast::node::String();
      str->setValue($1->c_str());
      str->setPosition(@1.copy_range());

      $$ = str;

      delete $1;
   }
%%

#include "mem/ast/node/Node.hpp"
